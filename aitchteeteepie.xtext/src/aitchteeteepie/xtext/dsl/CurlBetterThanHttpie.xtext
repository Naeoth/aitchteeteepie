grammar aitchteeteepie.xtext.dsl.CurlBetterThanHttpie with org.eclipse.xtext.common.Terminals hidden(ML_COMMENT, SL_COMMENT)

import "platform:/resource/aitchteeteepie/model/aitchteeteepie.ecore" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

CommandLineInterface returns CommandLineInterface:
	{CommandLineInterface}
	HTTP (WS flags+=Flag)* (WS method=Method)? WS (protocol=(ID|HTTP) '://')? ((url=URL (':' port=INT)?) | (':' (port=INT)?)) ('/' resource=(ID|HTTP))? (WS '--')? (WS items+=Item)*; //WS?

// FLAGS
Flag returns Flag:
	ProxyFlag | FormFlag | AuthFlag | DownloadFlag | BodyFlag | VersionFlag | JsonFlag | HelpFlag | HeadersFlag | VerboseFlag | TimeoutFlag;

HeadersFlag returns HeadersFlag:
	{HeadersFlag}
	('--header'|'-t');

BodyFlag returns BodyFlag:
	{BodyFlag}
	('--body'|'-b');
	
JsonFlag returns JsonFlag:
	{JsonFlag}
	('--json'|'-j');
	
FormFlag returns FormFlag:
	{FormFlag}
	('--form'|'-f');

ProxyFlag returns ProxyFlag:
	'--proxy' ('='|WS) ((protocol=(ID|HTTP) ':') | (proxyProtocol=(ID|HTTP) '://') | (protocol=(ID|HTTP) ':' proxyProtocol=(ID|HTTP) '://')) (username=(ID|HTTP) ':' password=(ID|HTTP) '@')? ((hostname=URL (':' port=INT)?) | (':' (port=INT)?));

AuthFlag returns AuthFlag:
	('--auth'|'-a') ('='|WS) username=(ID|HTTP) (':' password=(ID|HTTP))? ('@' (hostname=URL | ":"))?;

DownloadFlag returns DownloadFlag:
	{DownloadFlag}
	('--download'|'-d');
	
HelpFlag returns HelpFlag:
	{HelpFlag}
	('--help'|'-h');

VersionFlag returns VersionFlag:
	{VersionFlag}
	'--version';

VerboseFlag returns VerboseFlag:
	{VerboseFlag}
	('--verbose'|'-v');

TimeoutFlag returns TimeoutFlag:
	'--timeout' ('='|WS) timeout=FLOAT;

// METHODS
enum Method returns Method:
	GET='GET' | POST='POST' | PUT='PUT' | DELETE='DELETE';

// ITEMS
Item returns Item:
	 HttpHeaderItem | FormFileFieldItem | DataFieldItem | UrlParameterItem | RawJsonFieldItem;

HttpHeaderItem returns HttpHeaderItem:
	field=(ID|HTTP) ":" value=(ID|HTTP);

FormFileFieldItem returns FormFileFieldItem:
	field=(ID|HTTP) '@' value=(ID|HTTP);

DataFieldItem returns DataFieldItem:
	field=(ID|HTTP) '=' (value=(ID|HTTP) | '@' value=URL);

UrlParameterItem returns UrlParameterItem:
	field=(ID|HTTP) '==' value=(ID|HTTP);

RawJsonFieldItem returns RawJsonFieldItem:
	field=(ID|HTTP) ':=' ((value=JsonType) | ('@' value=JsonString));

Member returns Member:
	key=(ID|HTTP) WS? ':' WS? value=JsonType;

JsonType returns JsonType:
	JsonObject | JsonString | JsonArray | JsonBoolean | JsonNull | JsonNumber;

JsonObject returns JsonObject:
	{JsonObject}
	'{' ((WS? members+=Member) (WS? ',' WS? members+=Member)* WS?)? '}';

JsonString returns JsonString:
	value=(ID|HTTP);	

JsonArray returns JsonArray:
	{JsonArray}
	'[' ((WS? values+=JsonType) (WS? ',' WS? values+=JsonType)* WS?)? ']';

JsonBoolean returns JsonBoolean:
	value=BOOLEAN;

JsonNull returns JsonNull:
	{JsonNull}
	NULL;
	
JsonNumber returns JsonNumber:
	value=NUMBER;

terminal fragment DIGIT: '0'..'9';
@Override
terminal WS: (' '|'\t')+;
@Override
terminal INT returns ecore::EInt: '0' | ('1'..'9' DIGIT*);
terminal URL: "localhost" | ID '.' ID;
terminal HTTP: 'http';
terminal BOOLEAN returns ecore::EBoolean: 'true'|'false';
terminal NULL: 'null';
terminal FLOAT returns ecore::EFloat: INT | INT? '.' DIGIT* (('E'|'e') INT)?;
terminal NUMBER returns ecore::EFloat: '-'? DIGIT* '.' DIGIT* (('E'|'e') '-'? DIGIT*)?;